plugins {
	id 'org.springframework.boot' version "${springBootVersion}"
	id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
	id 'java'
    id 'com.google.cloud.tools.jib' version "${jibVersion}"
}

group = 'com.github.poad.examples'
version = '1.0.0'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation ('org.springframework.boot:spring-boot-starter-data-jpa') {
        exclude group: 'com.zaxxer', module: 'HikariCP'
    }
    // https://mvnrepository.com/artifact/org.apache.tomcat/tomcat-jdbc
    implementation "org.apache.tomcat:tomcat-jdbc:${tomcatJdbcVersion}"
	implementation 'org.springframework.boot:spring-boot-starter-web'

    // https://mvnrepository.com/artifact/org.springframework.security/spring-security-core
    implementation 'org.springframework.security:spring-security-core'

    // https://mvnrepository.com/artifact/com.webauthn4j/webauthn4j-core
    implementation "com.webauthn4j:webauthn4j-core:${webauthn4jVersion}"
    implementation "com.webauthn4j:webauthn4j-metadata:${webauthn4jVersion}"
    if (project.hasProperty('env')) {
        runtimeOnly "mysql:mysql-connector-java:${mysqlConnectorVersion}"
    } else {
        runtimeOnly 'com.h2database:h2'
    }
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

jib.from.image = 'mcr.microsoft.com/java/jre-headless:11-zulu-alpine'
jib.to.image = 'poad/webauthn-rp-example'
jib.to.tags = [ version ]
jib.container.creationTime = 'USE_CURRENT_TIMESTAMP'
jib.container.ports = [ '8080' ]
if (project.hasProperty('env')) {
    def env = "$env"
    jib.container.jvmFlags = [ "-Dspring.profiles.active=" + env ]
}
